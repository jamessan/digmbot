#!/usr/bin/gawk -f
#
# digmbot
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# version 2 as published by the Free Software Foundation.
#
# Copyright (c) 2013 Daniel Thau <paradigm@bedrocklinux.org>

BEGIN {
	setup()
	loadplugins()
	net = "/inet/tcp/0/" server "/" port
	IFS=OFS="\r\n"

	print  "Connecting to " server "/" port "..."
	print "NICK " nick                |& net
	print "USER " nick " x x :" nick  |& net

	while (net |& getline) {
		parse()
	}
}

function setup() {
	if (nick == "")      nick      = "digmbot"
	if (owner == "")     owner     = "paradigm"
	if (server == "")    server    = "chat.freenode.net"
	if (port == "")      port      = 6667
	if (plugindir == "") plugindir = "./plugins"
}

function loadplugins() {
	print "Loading plugins..."
	while (("ls "plugindir | getline plugin) > 0) {
		file = plugindir "/" plugin
		print "nick "      nick      |& file
		print "owner "     owner     |& file
		print "server "    server    |& file
		print "port "      port      |& file
		print "plugindir " plugindir |& file
		print "plugin "    plugin    |& file
		print "endload"              |& file
		close(file,"to")
		file |& getline plugins[plugin]
		close(file)
		print "Plugin "plugin" is looking for regex /" plugins[plugin] "/"
	}
	close("ls "plugindir)
}

function parse() {
	if ($1 == "PING")
		print "PONG "$2 |& net
	else
		print

	u = substr($0,2,index(substr($0,2),"!")-1) # user
	r = $3                                     # room
	$0 = substr($0,index(substr($0,2),":")+2)  # message
	#$0 = substr($0,1,length($0)-1)             # strip \r\n

	if (u == owner && r == nick) {
		if ($1 == "say") {
			print "PRIVMSG " $2 " :" cf(2) |& net
			return
		}
		if ($1 == "action") {
			print "PRIVMSG " $2 " :""\x01""ACTION " cf(2) |& net
			return
		}
		if ($1 == "join") {
			print "JOIN " $2 |& net
			return
		}
		if ($1 == "part") {
			print "PART " $2 |& net
			return
		}
		if ($1 == "quit")
			exit
	}
	for (plugin in plugins) {
		if ($0 ~ plugins[plugin]) {
			file = plugindir "/" plugin
			print "Triggered plugin: " file
			print "user " u     |& file
			print "room " r     |& file
			print "message " $0 |& file
			print "endtrigger"  |& file
			close(file,"to")
			file |& getline output
			close(file)
			if (output != "") {
				print "PRIVMSG " r " :" output |& net
				return
			}
		}
	}
}

# skips first n many fields, then returns the rest
function cf(n) {
	# couldn't get {n} in regex to work for some reason, here's an ugly hack.
	msg = $0
	regex = "^[[:blank:]]*"
	skipfield = "([^[:blank:]]+[[:blank:]]+)"
	for(i=0;i<n;i++)
		regex = regex "" skipfield
	sub(regex,"",msg)
	return msg
}
